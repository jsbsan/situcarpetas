' Gambas class file

Event ConseguirFoco(tag As Variant)

Event Copia(lista As String[], directorio As String)

Event PegarEn(Rutadestino As String)

Property DirectorioActual As String
Private hdirectorioActual As String

Function DirectorioActual_read() As String

  Return hdirectorioActual

End

Function DirectorioActual_write(valor As String)

  hdirectorioActual = valor
  FileView1.dir = valor
  DirView1.current = valor
  TextBoxRuta.text = valor

End

Public Sub DirView1_Click()

  FileView1.dir = DirView1.Current
  TextBoxRuta.text = DirView1.Current
  hDirectorioActual = DirView1.Current

End

Public Sub inicia()

  TextBoxRuta.text = DirView1.Current
  hDirectorioActual = DirView1.Current

  MenuDir.Visible = False
  MenuFile.visible = False

End

Public Sub FileView1_DblClick()

  Desktop.Open(DirView1.Current & "/" & FileView1.Current)

End

Public Sub ButtonIrA_Click()

  If Exist(TextBoxRuta.text) Then
    DirView1.Current = TextBoxRuta.text
    FileView1.dir = DirView1.Current
    hDirectorioActual = DirView1.Current

  Else
    Message.Info("No puedo ir a esa ruta")

  Endif

End

Public Sub FileView1_GotFocus()

  Raise ConseguirFoco(Me.tag)

End

Public Sub DirView1_GotFocus()

  Raise ConseguirFoco(Me.tag)

End

Public Sub TextBoxRuta_GotFocus()

  Raise ConseguirFoco(Me.tag)

End

Public Sub TextBoxRuta_KeyPress()

  If Key.code = Key.enter Or Key.code = Key.return Then
    DirView1.Current = TextBoxRuta.text
    FileView1.dir = DirView1.Current
    hdirectorioActual = DirView1.Current
  Endif

End

''-----------------------------------------------------
''-------  AREA DE LAS FUNCIONES DE LOS MENUS ---------
''-----------------------------------------------------

Public Sub terminalFile_Click()

  Exec ["xfce4-terminal", "--working-directory=" & TextBoxRuta.text]

End

Public Sub terminaldir_Click()

  terminalFile_Click()

End

Public Sub creardirectoriofile_Click()

  Dim nombre As String

  nombre = InputBox("Nombre de la nueva carpeta", "Nueva Carpeta")

  Try Mkdir DirView1.Current & "/" & nombre

End

Public Sub creardirectorio_Click()

  creardirectoriofile_Click()

End

Public Sub ButtonDirectorioFileview_Click()

  If FileView1.ShowDirectory = False Then
    FileView1.ShowDirectory = True
  Else
    FileView1.ShowDirectory = False
  Endif

End

Public Sub ButtonDetalles_Click()

  If FileView1.ShowDetailed = False Then
    FileView1.ShowDetailed = True
  Else
    FileView1.ShowDetailed = False
  Endif

End

Public Sub ButtonOcultos_Click()

  If FileView1.ShowHidden = False Then
    FileView1.ShowHidden = True
  Else
    FileView1.ShowHidden = False
  Endif

End

Public Sub refrescarfile_Click()

  FileView1.Reload()

End

Public Sub refrescardir_Click()

  DirView1.Reload()

End

Public Sub copiarfichero_Click()

  Print FileView1.Selection

  Raise copia(FileView1.Selection, hDirectorioActual)

End

Public Sub pegarfichero_Click()

  Raise PegarEn(hdirectorioActual)

End

Public Sub reload()

  FileView1.Reload()

  DirView1.Reload()

End

Public Sub borrarfile_Click()

  Dim respuesta As Integer
  Dim f As String

  respuesta = Message.Question("Borrar el fichero:\n" & FileView1.Selection.Join("\n"), "Si", "No")

  If respuesta = 1 Then

    For Each f In FileView1.Selection
      Kill DirView1.Current & "/" & f
    Next
    reload()
  Endif

End

Public Sub abrirnavegador_Click()

  ''TODO: ABRIR NAVEGADOR

End

Public Sub renombrarfile_Click()

  ''TODO: RENOMBRAR FICHERO

End

Public Sub renombrardir_Click()
  ''TODO: RENOMBRAR DIRECTORIO

End
